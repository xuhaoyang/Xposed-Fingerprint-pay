apply plugin: 'com.android.application'

def ADB_PATH = "${android.getSdkDirectory()}/platform-tools/adb"

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.yyxx.wechatfp"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 7
        versionName "2.0.0"
        buildConfigField "String", "APP_SETTINGS_NAME", "\"指紋設置\""
        buildConfigField "String", "APP_PRODUCT_NAME", "\"WeChatFp\""
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    repositories{
        flatDir{
            dirs 'libs'
        }
    }

    packagingOptions {
        exclude 'publicsuffixes.gz'
    }
}


android.applicationVariants.all { variant ->
    variant.getCompileConfiguration().resolutionStrategy {
        if (variant.install != null) {
            variant.install.doLast {
                println "Running app..."
                exec {
                    executable = ADB_PATH
                    args = ['shell', "am start -n ${variant.applicationId}/.activity.HomeActivity"]
                }
            }
        }
        variant.outputs.each { output ->
            def apkName = "WeChatFp-${android.defaultConfig.versionName}-${variant.buildType.name}.apk"
            output.outputFileName = apkName
        }
    }
}

task stopWeChat {
    doLast {
        println "Stop WeChat..."
        exec {
            executable = ADB_PATH
            args = ['shell', "am force-stop com.tencent.mm"]
        }
    }
}

task debugWeChat(dependsOn: ["installDebug", stopWeChat]) {
    doLast {
        println "Start WeChat..."
        exec {
            executable = ADB_PATH
            args = ['shell', "am start -n com.tencent.mm/com.tencent.mm.ui.LauncherUI"]
        }

    }
}

task stopAlipay {
    doLast {
        println "Stop Alipay..."
        exec {
            executable = ADB_PATH
            args = ['shell', "am force-stop com.eg.android.AlipayGphone"]
        }
    }
}

task debugAlipay(dependsOn: ["installDebug", stopAlipay]) {
    doLast {
        println "Start WeChat..."
        exec {
            executable = ADB_PATH
            args = ['shell', "am start -n com.eg.android.AlipayGphone/com.eg.android.AlipayGphone.AlipayLogin"]
        }

    }
}

dependencies {
    implementation 'com.android.support:support-v4:26.0.2'
    implementation 'com.wei.android.lib:fingerprintidentify:1.2.1'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'
    provided 'de.robv.android.xposed:api:53'
}